apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.apollographql.apollo'

apollo {
    generateKotlinModels = true
    customTypeMapping = [
            "URL"      : "kotlin.String",
            "LocalDate": "org.threeten.bp.LocalDate",
            "Upload"   : "com.apollographql.apollo.api.FileUpload"
    ]
}

apply plugin: 'com.jaredsburrows.license'
licenseReport {
    copyHtmlReportToAssets = true
}

android {
    androidExtensions {
        experimental = true
    }
    compileSdkVersion 29

    defaultConfig {
        targetSdkVersion 29
        applicationId 'com.hedvig'

        minSdkVersion 21
        versionCode 43
        versionName '3.3.0'

        resValue "string", "build_config_package", "com.hedvig.app"
        vectorDrawables.useSupportLibrary = true

        buildConfigField("String", "DEFAULT_LANGUAGE", "\"${rootProject.ext.defaultLanguage}\"")
        buildConfigField("String[]", "EXCLUDED_LANGUAGES", "{\"${rootProject.ext.excludedLanguages.join("\",\"")}\"}")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildTypes {
        release {
            applicationIdSuffix '.app'

            buildConfigField "String", "APP_ID", "\"com.hedvig.app\""
            buildConfigField "String", "GRAPHQL_URL", "\"https://giraffe.hedvig.com/graphql\""
            buildConfigField "String", "WS_GRAPHQL_URL", "\"wss://giraffe.hedvig.com/subscriptions\""
            buildConfigField "String", "BASE_URL", "\"https://giraffe.hedvig.com\""
            buildConfigField "String", "REFERRALS_LANDING_BASE_URL", "\"https://www.hedvig.com/referrals/\""
            resValue "string", "file_provider_authority", "\"com.hedvig.android.file.provider\""

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        staging {
            applicationIdSuffix '.test.app'

            buildConfigField "String", "APP_ID", "\"com.hedvig.test.app\""
            buildConfigField "String", "GRAPHQL_URL", "\"https://graphql.dev.hedvigit.com/graphql\""
            buildConfigField "String", "WS_GRAPHQL_URL", "\"wss://graphql.dev.hedvigit.com/subscriptions\""
            buildConfigField "String", "BASE_URL", "\"https://graphql.dev.hedvigit.com\""
            buildConfigField "String", "REFERRALS_LANDING_BASE_URL", "\"https://hedvig-web-onboarding-staging.herokuapp.com/referrals/\""
            resValue "string", "file_provider_authority", "\"com.hedvig.android.test.file.provider\""

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix '.dev.app'

            buildConfigField "String", "APP_ID", "\"com.hedvig.dev.app\""
            buildConfigField "String", "GRAPHQL_URL", "\"https://graphql.dev.hedvigit.com/graphql\""
            buildConfigField "String", "WS_GRAPHQL_URL", "\"wss://graphql.dev.hedvigit.com/subscriptions\""
            buildConfigField "String", "BASE_URL", "\"https://graphql.dev.hedvigit.com\""
            buildConfigField "String", "REFERRALS_LANDING_BASE_URL", "\"https://hedvig-web-onboarding-staging.herokuapp.com/referrals/\""

            resValue "string", "file_provider_authority", "\"com.hedvig.android.dev.file.provider\""

            debuggable true
        }
    }
}

dependencies {
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    implementation 'nl.littlerobots.rxlint:rxlint:1.7.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.media:media:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'
    implementation "androidx.preference:preference-ktx:1.1.0"
    implementation "androidx.core:core-ktx:1.2.0"
    implementation 'com.google.android:flexbox:2.0.1'
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    // Android lifecycle
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.2.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"

    // WorkManager
    def workmanager_version = "2.3.2"
    implementation "androidx.work:work-runtime:$workmanager_version"
    implementation "androidx.work:work-runtime-ktx:$workmanager_version"

    // Okhttp
    def okhttp3_version = '4.0.1'
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3_version"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$okhttp3_version"

    // Apollo
    implementation "com.apollographql.apollo:apollo-runtime:$apollo_version"
    implementation "com.apollographql.apollo:apollo-android-support:$apollo_version"
    implementation "com.apollographql.apollo:apollo-rx2-support:$apollo_version"
    implementation "com.apollographql.apollo:apollo-coroutines-support:$apollo_version"


    // Firebase
    implementation 'com.google.android.gms:play-services-base:17.1.0'
    releaseImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    stagingImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    implementation "com.google.firebase:firebase-core:17.2.3"
    implementation "com.google.firebase:firebase-dynamic-links:19.1.0"
    implementation 'com.google.firebase:firebase-config:19.1.2'
    implementation "com.google.firebase:firebase-messaging:20.1.2"

    // Koin
    def koin_version = "2.0.1"
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-android-viewmodel:$koin_version"

    implementation 'net.ypresto.timbertreeutils:timbertreeutils:1.0.0'

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.github.PaulWoitaschek:Slimber:1.0.7'

    // Lottie
    implementation "com.airbnb.android:lottie:3.0.7"

    // Exoplayer
    implementation 'com.google.android.exoplayer:exoplayer:2.10.3'

    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.0'

    // coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0'

    // Svg
    implementation 'com.caverock:androidsvg-aar:1.4'

    // Glide
    implementation "com.github.bumptech.glide:glide:4.9.0"
    kapt 'com.github.bumptech.glide:compiler:4.9.0'
    implementation("com.github.bumptech.glide:recyclerview-integration:4.9.0") {
        transitive = false
    }

    //facebook
    implementation 'com.facebook.android:facebook-android-sdk:5.15.3'

    // ZXing
    implementation 'com.google.zxing:core:3.4.0'

    // insetter
    implementation "dev.chrisbanes:insetter:0.2.0"
    implementation "dev.chrisbanes:insetter-ktx:0.2.0"

    // markwon
    implementation "io.noties.markwon:core:4.2.1"

    // adyen
    implementation 'com.adyen.checkout:drop-in:3.6.4'

    // test
    testImplementation 'junit:junit:4.12'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.2'
}

apply plugin: 'com.google.gms.google-services'
googleServices {
    disableVersionCheck = true
}

apply plugin: 'com.hedvig.textkeys'

textkeys {
    graphqlEndpoint = 'https://graphql.dev.hedvigit.com/graphql'
    defaultLanguageCode = rootProject.ext.defaultLanguage
    excludedLanguages = rootProject.ext.excludedLanguages
}

task updateSchema(type: Exec) {
    group = 'GraphQL'
    description = 'Fetches the latest schema from our dev GraphQL-server'

    commandLine 'apollo', 'schema:download', '--endpoint=https://graphql.dev.hedvigit.com/graphql', "$projectDir/src/main/graphql/com/hedvig/android/owldroid/schema.json"
}
