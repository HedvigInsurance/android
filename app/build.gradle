apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.likandr.lokalise'

apply plugin: 'com.jaredsburrows.license'
licenseReport {
    copyHtmlReportToAssets = true
}

android {
    androidExtensions {
        experimental = true
    }
    buildFeatures {
        viewBinding true
    }
    compileSdkVersion 29

    defaultConfig {
        targetSdkVersion 29
        applicationId 'com.hedvig'

        minSdkVersion 23
        versionCode 43
        versionName '4.3.0'

        resValue "string", "build_config_package", "com.hedvig.app"
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "com.hedvig.app.TestRunner"
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    packagingOptions {
        exclude "javamoney.properties"
    }

    buildTypes {
        release {
            applicationIdSuffix '.app'

            buildConfigField "String", "APP_ID", "\"com.hedvig.app\""
            buildConfigField "String", "GRAPHQL_URL", "\"https://giraffe.hedvig.com/graphql\""
            buildConfigField "String", "WS_GRAPHQL_URL", "\"wss://giraffe.hedvig.com/subscriptions\""
            buildConfigField "String", "BASE_URL", "\"https://giraffe.hedvig.com\""
            buildConfigField "String", "WEB_BASE_URL", "\"https://www.hedvig.com/\""
            resValue "string", "file_provider_authority", "\"com.hedvig.android.file.provider\""

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        staging {
            applicationIdSuffix '.test.app'

            buildConfigField "String", "APP_ID", "\"com.hedvig.test.app\""
            buildConfigField "String", "GRAPHQL_URL", "\"https://graphql.dev.hedvigit.com/graphql\""
            buildConfigField "String", "WS_GRAPHQL_URL", "\"wss://graphql.dev.hedvigit.com/subscriptions\""
            buildConfigField "String", "BASE_URL", "\"https://graphql.dev.hedvigit.com\""
            buildConfigField "String", "WEB_BASE_URL", "\"https://www.dev.hedvigit.com/\""
            resValue "string", "file_provider_authority", "\"com.hedvig.android.test.file.provider\""

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix '.dev.app'

            buildConfigField "String", "APP_ID", "\"com.hedvig.dev.app\""
            buildConfigField "String", "GRAPHQL_URL", "\"https://graphql.dev.hedvigit.com/graphql\""
            buildConfigField "String", "WS_GRAPHQL_URL", "\"wss://graphql.dev.hedvigit.com/subscriptions\""
            buildConfigField "String", "BASE_URL", "\"https://graphql.dev.hedvigit.com\""
            buildConfigField "String", "WEB_BASE_URL", "\"https://www.dev.hedvigit.com/\""

            resValue "string", "file_provider_authority", "\"com.hedvig.android.dev.file.provider\""

            debuggable true
        }
    }

    sourceSets {
        debug {
            java.srcDirs += "src/engineering/java"
            res.srcDirs += "src/engineering/res"
            manifest.srcFile "src/debug/AndroidManifest.xml"
        }
        staging {
            java.srcDirs += "src/engineering/java"
            res.srcDirs += "src/engineering/res"
            manifest.srcFile "src/debug/AndroidManifest.xml"
        }
    }
}

dependencies {
    implementation project(':apollo')

    androidTestImplementation project(':testdata')
    debugImplementation project(':testdata')
    stagingImplementation project(':testdata')

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.media:media:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.2.0'
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.core:core-ktx:1.3.1"
    implementation 'com.google.android:flexbox:2.0.1'
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    // Android lifecycle
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.2.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"

    implementation 'com.github.Zhuinden:livedata-combinetuple-kt:1.0.0'

    // WorkManager
    def workmanager_version = "2.4.0"
    implementation "androidx.work:work-runtime:$workmanager_version"
    implementation "androidx.work:work-runtime-ktx:$workmanager_version"

    // Okhttp
    def okhttp3_version = '4.7.2'
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3_version"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:4.8.0"

    // Firebase
    implementation 'com.google.android.gms:play-services-base:17.3.0'
    releaseImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    stagingImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    implementation "com.google.firebase:firebase-dynamic-links:19.1.0"
    implementation 'com.google.firebase:firebase-config:19.2.0'
    implementation "com.google.firebase:firebase-messaging:20.2.4"

    implementation "com.mixpanel.android:mixpanel-android:5.8.4"

    // Koin
    def koin_version = "2.1.5"
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-android-viewmodel:$koin_version"

    implementation 'net.ypresto.timbertreeutils:timbertreeutils:1.0.0'

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Slimber
    implementation 'com.github.PaulWoitaschek:Slimber:1.0.7'

    // Lottie
    implementation "com.airbnb.android:lottie:3.0.7"

    // Exoplayer
    implementation 'com.google.android.exoplayer:exoplayer:2.10.3'

    // coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0'

    // ReactiveX
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'

    // Svg
    implementation 'com.caverock:androidsvg-aar:1.4'

    // Glide
    implementation "com.github.bumptech.glide:glide:4.9.0"
    kapt 'com.github.bumptech.glide:compiler:4.9.0'
    implementation("com.github.bumptech.glide:recyclerview-integration:4.9.0") {
        transitive = false
    }

    //facebook
    implementation 'com.facebook.android:facebook-android-sdk:5.15.3'

    // ZXing
    implementation 'com.google.zxing:core:3.4.0'

    // insetter
    implementation "dev.chrisbanes:insetter:0.2.1"
    implementation "dev.chrisbanes:insetter-ktx:0.2.1"

    // markwon
    implementation "io.noties.markwon:core:4.2.1"

    // adyen
    implementation 'com.adyen.checkout:drop-in:3.6.4'

    // JSR-354
    implementation 'org.javamoney:moneta:1.3'

    // shimmer
    implementation 'com.facebook.shimmer:shimmer:0.5.0'

    // test
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation 'com.agoda.kakao:kakao:2.3.4'
    androidTestImplementation 'org.powermock:powermock-core:2.0.6'
    androidTestImplementation "com.apollographql.apollo:apollo-idling-resource:$apollo_version"
    androidTestImplementation 'com.willowtreeapps.assertk:assertk-jvm:0.22'
    androidTestImplementation "org.koin:koin-test:$koin_version"
    androidTestImplementation 'org.awaitility:awaitility:3.1.6'
    androidTestImplementation 'org.awaitility:awaitility-kotlin:3.1.6'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.2'
}

apply plugin: 'com.google.gms.google-services'
googleServices {
    disableVersionCheck = true
}

def lokaliseProperties = new Properties()
lokaliseProperties.load(new FileInputStream(rootProject.file("lokalise.properties")))

lokalise {
    id lokaliseProperties['id']
    token lokaliseProperties['token']
}

